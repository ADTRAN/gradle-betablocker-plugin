/*
 * Copyright 2018 ADTRAN, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
            mavenCentral()
        }
    }

    dependencies {
        classpath "com.netflix.nebula:gradle-dependency-lock-plugin:11.+"
        classpath "com.gradle.publish:plugin-publish-plugin:0.14.0"
    }
}

plugins {
    id "nebula.dependency-lock" version "11.2.0"
    id "net.saliman.cobertura" version "4.0.0"
    id "groovy"
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.14.0"
    id "maven-publish"
    id "signing"
}

group = artifact_group
version = get_version()

sourceCompatibility = 1.6
targetCompatibility = 1.6

gradlePlugin {
    plugins {
        betablockerPlugin {
            id = "${group}.${artifact_name}"
            implementationClass = "com.adtran.BetablockerPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/ADTRAN/gradle-betablocker-plugin'
    vcsUrl = 'https://github.com/ADTRAN/gradle-betablocker-plugin'
    description = 'Gradle plugin to prevent non-stable versions of dependencies from being inadvertently consumed by a project'
    tags = ['dependencies']
    plugins {
        betablockerPlugin {
            id = "${group}.${artifact_name}"
            displayName = 'Betablocker Plugin'
        }
    }
}

dependencyLock {
    includeTransitives = true
}
task freeze(dependsOn: ['generateLock', 'saveLock'])

cobertura {
    coverageFormats = ['html', 'xml']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation localGroovy()

    testImplementation 'junit:junit:4.12'
}

def pomConfig = {
    scm {
        connection 'scm:git:https://github.com/ADTRAN/gradle-betablocker-plugin.git'
        developerConnection 'scm:git:https://github.com/ADTRAN/gradle-betablocker-plugin.git'
        url 'https://github.com/ADTRAN/gradle-betablocker-plugin'
    }

    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            name 'Nathan Alderson'
            email 'nathan.alderson@adtran.com'
            organization 'ADTRAN, Inc.'
            organizationUrl 'http://www.adtran.com/'
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from groovydoc
    from 'README.rst'
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            name = "ossrh"
            url = version.toString().endsWith('-SNAPSHOT') ? releaseRepo: snapshotRepo
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }

    publications {
        betablocker(MavenPublication) {
            afterEvaluate {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId "$groupId"
                artifactId 'betablocker-plugin'
                version "$version"
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'Gradle plugin to prevent non-stable versions of dependencies from being inadvertently consumed by a project')
                    root.appendNode('name', "$artifact_group:$artifact_name")
                    root.appendNode('url', 'https://github.com/ADTRAN/gradle-betablocker-plugin')
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingKeyPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.betablocker
}

groovydoc.overviewText = resources.text.fromString('Please see <a href="README.rst">README.rst</a>')

assemble.dependsOn("sourcesJar", "javadocJar")

artifacts {
    archives sourcesJar, javadocJar
}

def configureTestTask(Task task) {
    task.outputs.upToDateWhen { false }
    task.testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }
}
configureTestTask(project.tasks.test)

publish.dependsOn publishPlugins

def get_version() {
    def cmd = ["git", "tag", "--sort=-version:refname", "-l", "v$artifact_version*"]
    def proc = cmd.execute()
    proc.waitFor()
    assert(proc.exitValue() == 0)
    def latest = proc.text.split("\n")[0]
    def tokens = latest?.tokenize(".")
    def bugfix = tokens ? tokens?.last()?.toInteger() : -1
    def suffix = System.getenv("CI") ? "" : "dev0"
    "${artifact_version}.${bugfix+1}${suffix}"
}

task printVersion() { doLast { println(version) } }

task gitTag() {
  description = "Add and push a git tag with the project's version (only in CI)."
  if(System.getenv("CI")) {
    doLast {
      def cmd = ["git", "tag", "-m", "automatic tag for $version", "v${version}"]
      def proc = cmd.execute()
      proc.waitFor()
      assert(proc.exitValue() == 0)
      cmd = ["git", "push", "v${version}"]
      proc = cmd.execute()
      proc.waitFor()
      assert(proc.exitValue() == 0)
    }
  }
}
